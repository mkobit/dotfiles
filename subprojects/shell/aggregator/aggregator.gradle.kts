import dotfilesbuild.io.file.EditFile
import dotfilesbuild.io.file.content.SetContent

plugins {
  dotfilesbuild.`dotfiles-lifecycle`
}

val bin by configurations.creating {
  isCanBeConsumed = false
  isCanBeResolved = true
  attributes {
    attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage::class, "bin"))
  }
}

dependencies {
  bin(project(":kubectl"))
}

tasks {
  val generatePathZshrcFile by registering(EditFile::class) {
    inputs.files(bin)
    description = "Generates a ZSH file than be sourced to expand the path to include "
    file.set(layout.buildDirectory.file("zsh/extend_path"))
    editActions.set(
      listOf(
        SetContent {
          """export PATH="${'$'}{PATH}${bin.resolve().joinToString(separator = ":", prefix = ":")}" # generated by Gradle"""
        }
      )
    )
  }

  "dotfiles" {
    dependsOn(generatePathZshrcFile)
  }
}

